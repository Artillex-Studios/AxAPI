plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta10'
}

group = 'com.artillexstudios.axapi'
version = property("version")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(path: ":api")
    implementation project(path: ":folia")
    implementation project(path: ":nms:v1_21_R5", configuration: "reobf")
    implementation project(path: ":nms:v1_21_R4", configuration: "reobf")
    implementation project(path: ":nms:v1_21_R3", configuration: "reobf")
    implementation project(path: ":nms:v1_21_R2", configuration: "reobf")
    implementation project(path: ":nms:v1_21_R1", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R4", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R3", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R2", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R1", configuration: "reobf")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.gradleup.shadow'

    tasks {
        compileJava {
            options.compilerArgs += ["-parameters"]
            options.fork = true
            options.encoding = 'UTF-8'
        }
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        dependsOn(clean)
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()

        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven {
            name = 'JitPack'
            url = 'https://jitpack.io/'
        }

        maven {
            name = 'Artillex-Studios'
            url = 'https://repo.artillex-studios.com/releases/'
        }

        maven {
            name = 'PlaceholderAPI'
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
    }

    dependencies {
        implementation("com.github.NEZNAMY:YamlAssist:1.0.5") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        implementation("dev.dejvokep:boosted-yaml:1.3.7") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

//        implementation("net.kyori:adventure-text-serializer-legacy:4.22.0")
//        implementation("net.kyori:adventure-text-serializer-plain:4.22.0")
//        implementation("net.kyori:adventure-text-serializer-gson:4.22.0")
//        implementation("net.kyori:adventure-text-minimessage:4.14.0")
        implementation("io.github.revxrsal:zapper.api:1.1.5")
        implementation("net.kyori:adventure-api:4.22.0")
        implementation("com.zaxxer:HikariCP:6.3.0")
        implementation("org.yaml:snakeyaml:2.2")
        compileOnly("com.github.ben-manes.caffeine:caffeine:2.9.2")
        compileOnly("org.apache.commons:commons-math3:3.6.1")
        compileOnly("it.unimi.dsi:fastutil-core:8.5.12")
        compileOnly("me.clip:placeholderapi:2.11.5")
        compileOnly("it.unimi.dsi:fastutil:8.5.12")

        implementation("com.github.Artillex-Studios.Lamp:common:c60ac3c398") {
            exclude group: 'net.kyori', module: 'adventure'
        }
        implementation("com.github.Artillex-Studios.Lamp:bukkit:c60ac3c398") {
            exclude group: 'net.kyori', module: 'adventure'
        }
    }

    publishing {
        afterEvaluate {
            repositories {
                maven {
                    name = "Artillex-Studios"
                    url = "https://repo.artillex-studios.com/releases"
                    credentials {
                        username = "${maven_username}"
                        password = "${maven_password}"
                    }
                }
            }

            publications {
                shadow(MavenPublication) { publication ->
                    project.shadow.component(publication)
                    artifactId = "axapi"

                    println("Project: ${project.name}")
                    if (project.name == "api") {
                        artifact tasks.javadocJar
                        println("Publishing: ${project.name}")
                    }
                }
            }
        }
    }

    publishToMavenLocal {
        dependsOn {
            build
        }
    }
}

tasks.build.dependsOn(shadowJar)

shadowJar {
    relocate('dev.dejvokep.boostedyaml', 'com.artillexstudios.axapi.libs.boostedyaml')
    relocate('me.neznamy.yamlassist', 'com.artillexstudios.axapi.libs.yamlassist')
    relocate('org.apache.commons.math3', 'com.artillexstudios.axapi.libs.math3')
    relocate('com.github.benmanes', 'com.artillexstudios.axapi.libs.caffeine')
    relocate('org.yaml.snakeyaml', 'com.artillexstudios.axapi.libs.snakeyaml')
    relocate('com.zaxxer.hikari', 'com.artillexstudios.axapi.libs.hikari')
}

allprojects.forEach {
    it.afterEvaluate {
        println(it.name + " " + it.group + " " + it.version)
    }
}