plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.artillexstudios.axapi'
version = '1.4.77'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    implementation project(path: ":api")
    implementation project(path: ":folia")
    implementation project(path: ":nms:v1_20_R4", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R3", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R2", configuration: "reobf")
    implementation project(path: ":nms:v1_20_R1", configuration: "reobf")
    implementation project(path: ":nms:v1_19_R3", configuration: "reobf")
    implementation project(path: ":nms:v1_19_R2", configuration: "reobf")
    implementation project(path: ":nms:v1_19_R1", configuration: "reobf")
    implementation project(path: ":nms:v1_18_R2", configuration: "reobf")
    implementation project(path: ":nms:v1_18_R1", configuration: "reobf")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks {
        compileJava {
            options.compilerArgs += ["-parameters"]
            options.fork = true
            options.encoding = 'UTF-8'
        }
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        dependsOn(clean)
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()

        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven {
            name = 'JitPack'
            url = 'https://jitpack.io/'
        }

        maven {
            name = 'Libby'
            url = 'https://repo.alessiodp.com/releases/'
        }

        maven {
            name = 'PlaceholderAPI'
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
    }

    dependencies {
        implementation("com.github.NEZNAMY:YamlAssist:1.0.5") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        implementation("dev.dejvokep:boosted-yaml:1.3") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        implementation("net.byteflux:libby-bukkit:1.3.0")
        implementation("net.kyori:adventure-platform-bukkit:4.3.0")
        implementation("net.kyori:adventure-text-minimessage:4.14.0")
        implementation("net.kyori:adventure-text-serializer-plain:4.14.0")
        compileOnly("it.unimi.dsi:fastutil-core:8.5.12")
        compileOnly("org.apache.commons:commons-math3:3.6.1")
        compileOnly("com.github.ben-manes.caffeine:caffeine:2.9.2")
        compileOnly("me.clip:placeholderapi:2.11.5")

        implementation("com.github.Revxrsal.Lamp:common:3.1.9") {
            exclude group: 'net.kyori', module: 'adventure'
        }
        implementation("com.github.Revxrsal.Lamp:bukkit:3.1.9") {
            exclude group: 'net.kyori', module: 'adventure'
        }
    }

    publishing {
        afterEvaluate {
            repositories {
                maven {
                    name = "Artillex-Studios"
                    url = "https://repo.artillex-studios.com/releases"
                    credentials {
                        username = "${maven_username}"
                        password = "${maven_password}"
                    }
                }
            }

            publications {
                shadow(MavenPublication) { publication ->
                    project.shadow.component(publication)
                    artifactId = "axapi"
                }   
            }
        }
    }

    publishToMavenLocal {
        dependsOn {
            build
        }
    }
}

tasks.build.dependsOn(shadowJar)

shadowJar {
    relocate('me.neznamy.yamlassist', 'com.artillexstudios.axapi.libs.yamlassist')
    relocate('dev.dejvokep', 'com.artillexstudios.axapi.libs.boostedyaml')
    relocate('net.byteflux.libby', 'com.artillexstudios.axapi.libs.libby')
}

allprojects.forEach { it.afterEvaluate {
println(it.name + " " + it.group + " " + it.version)
}  }