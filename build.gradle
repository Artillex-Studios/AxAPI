plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.artillexstudios.axapi'
version = '1.0-SNAPSHOT'

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
    implementation project(path: ":api")
    implementation project(path: ":common")
    implementation project(path: ":nms:v1_20_R1", configuration: "reobf")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks {
        compileJava {
            options.encoding = 'UTF-8'
            options.release.set(17)
        }
    }

    compileJava {
        dependsOn(clean)
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()

        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven {
            name = 'JitPack'
            url = 'https://jitpack.io/'
        }
    }

    dependencies {
        compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT") {
            exclude group: 'net.kyori', module: 'adventure'
        }

        implementation("com.github.NEZNAMY:YamlAssist:1.0.5")

        implementation("dev.dejvokep:boosted-yaml:1.3") {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        compileOnly("it.unimi.dsi:fastutil:8.5.12")

        compileOnly("net.kyori:adventure-platform-bukkit:4.3.0")
        compileOnly("net.kyori:adventure-text-minimessage:4.14.0")
        implementation("com.github.ReflxctionDev:PluginLib:1.3")
    }
}

shadowJar {
    relocate("it.unimi.dsi", "com.artillexstudios.axapi.libs.fastutil")
    relocate("net.kyori", "com.artillexstudios.axapi.libs.kyori")
    archiveClassifier.set('')
}

build {
    dependsOn(shadowJar)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.artillex-studios'
            artifactId = 'AxAPI'
            version = '1.0-SNAPSHOT'

            from components.java
        }
    }
}